
%{
#include <iostream>
#include <cstdlib>
#include <string>
#include <map>

using namespace std;

#define YYDEBUG (1)

extern int yylex();
void yyerror(string msg) {
	cerr << msg << endl;
	exit(1);
}

map<string, string> id_table;
%}

%union {
	string* s;
	int num;
}

%token AsgnOp Print Lower Upper Reverse Index
%token <s> Str
%token <s> Id
%token <num> Num

%type <s> StrExp
%type <num> IntExp

%left '+' '-'
%left '*'

%start Prog
%%

Prog : Line '\n' Prog
| Line
| %empty
;

Line : Id AsgnOp StrExp {
	id_table[*$1] = *$3;
	delete $3;
	delete $1;
}
| Print '(' StrExp ')' {
	cout << *$3 << endl;
	delete $3;
}
| Print'('IntExp')' {
	cout << $3 << endl;
}
| '\n'
;

IntExp: '|'StrExp'|' {
	$$ = $2->size();
	delete $2;
}
| Index '(' StrExp ',' StrExp ')' {
	$$ = $3->find(*$5);	
}
| Num {
	$$ = $1;
}
;

StrExp : Str {
	$$ = $1;
}
| Id {
	$$ = new string(id_table[*$1]);
	delete $1;
}
| '(' StrExp ')' {
	$$ = $2;
}
| StrExp '+' StrExp {
	$$ = new string("");
	*$$ = *$1 + *$3;
	delete $1;
	delete $3;
}
| IntExp '*' StrExp {
	$$ = new string("");
	for(int i = 0; i < $1; i += 1) {
		*$$ += *$3;
	}
	delete $3;
}
| StrExp '*' IntExp {
	$$ = new string("");
	for(int i = 0; i < $3; i += 1) {
		*$$ += *$1;
	}
	delete $1;
}
| Id '[' IntExp ':' IntExp ']' {
	$$ = new string("");
	string s = id_table[*$1];
	for(int i = $3; i < $5; i += 1) {
		$$->push_back(s[i]);
	}
	delete $1;
}
| Id '[' ':' IntExp ']' {
	$$ = new string("");
	string s = id_table[*$1];
	for(int i = 0; i < $4; i += 1) {
		$$->push_back(s[i]);
	}
	delete $1;
}
| Id '[' IntExp ':' ']' {
	$$ = new string("");
	string s = id_table[*$1];
	int n = s.size();
	for(int i = $3; i < n; i += 1) {
		$$->push_back(s[i]);
	}
	delete $1;
}
| Lower '(' StrExp ')' {
	$$ = new string("");
	for(char c: *$3) {
		$$->push_back(tolower(c));
	}
	delete $3;
}
| Upper '(' StrExp ')' {
	$$ = new string("");
	for(char c: *$3) {
		$$->push_back(toupper(c));
	}
	delete $3;
}
| Reverse '(' StrExp ')' {
	$$ = new string("");
	int n = $3->size();
	for(int i=n-1;i>=0;i-=1) {
		$$->push_back((*$3)[i]);
	}
	delete $3;
}
;

%%

int main() {
	//yydebug = 1;
	yyparse();
	return 0;
}
